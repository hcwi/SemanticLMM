PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX stato: <http://purl.obolibrary.org/obo/stato.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# Which variables were used to explain 'yield' trait, and in which datasets?
# Returns models with independent variables (or their combinations) used to explain a particular dependent variable, a corresponding p-value, plus a dataset for which the analysis was done, ordered by significance.

SELECT DISTINCT 
	?Trait ?Variables 
	(round(?Pval*1000)/1000 as ?Pvalue)
	?Dataset ?model 
	(GROUP_CONCAT(DISTINCT ?Var; SEPARATOR = ", ") AS ?Variables)
WHERE { 
    
    FILTER (regex(?Trait, "yield")). ### <----------- HERE SPECIFY A DEPENDENT VARIABLE
    
    ?model rdf:type obo:STATO_0000464. # mixed model
    ?model stato:is_model_for/rdfs:label ?Trait.
    ?model obo:BFO_0000051 ?term. # has part
    ?term obo:IAO_0000136 ?var. # is about
	?var rdf:type obo:STATO_0000258. # variable
    ?var rdfs:label ?Var.
    ?hypo obo:IAO_0000136 ?term. # is about
    ?hypo rdf:type obo:STATO_0000065. #hypothesis
    optional {
        ?pval obo:IAO_0000136 ?hypo. # is about
    	?pval rdf:type obo:OBI_0000175. #pvalue
    	?pval rdf:value ?Pval.
    }
    
    ?dataset rdfs:label ?Dataset.
    ?dataset rdf:type obo:IAO_0000100. #dataset
    ?modelfitting obo:OBI_0000293 ?dataset. #hasInput
	?modelfitting rdf:type obo:STATO_0000218. #modelFitting
    ?modelfitting obo:OBI_0000293 ?model. #hasInput
    
}
GROUP BY ?model ?Trait ?Pval ?Dataset
ORDER BY ?Pvalue