PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX stato: <http://purl.obolibrary.org/obo/stato.owl#>
PREFIX tmp: <http://igr.poznan.pl/lmm/UNKNOWN_TO_BE_REPLACED#>

# Find most significant random effects
# Returns variance parameters representing the biggest variability of a specified trait, plus the type of covariance model assumed. Ordered by descending variance.


SELECT DISTINCT 
	?Trait ?Var 
	(replace(?CovstrType, "covariance structure", "") as ?CovarianceModel)
	(round(?Varparam*1000)/1000 as ?Variance)
	?model
WHERE { 
    ?model obo:BFO_0000051 ?term.
    ?model rdf:type obo:STATO_0000464. #lmm
    ?model stato:is_model_for/rdfs:label ?Trait.
    
    FILTER(regex(?Trait, "")) ### <---------- HERE SPECIFY THE DEPENDENT VARIABLE
    
    ?term rdf:type obo:STATO_0000467. #random term
    ?term obo:IAO_0000136 ?var.
    ?var rdf:type obo:OBI_0000750. #independent variable
    ?var rdfs:label ?Var.
    ?term obo:BFO_0000051 ?covstr.
    ?covstr rdf:type obo:STATO_0000346. #covariance structure
    ?covstr rdf:type ?covstrType.
    ?covstrType rdfs:label ?CovstrType.
    ?covstrType rdfs:subClassOf obo:STATO_0000346.
    ?covstr obo:BFO_0000051 ?varparam. #has part
    ?varparam rdf:type obo:STATO_0000034. #model parameter
    ?varparam rdf:type obo:STATO_0000113. #variance
    ?varparam ^obo:STATO_0000403/rdf:value ?Varparam. #estimator_of
}
ORDER BY ?Trait desc(?Variance)