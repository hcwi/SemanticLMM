PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lmm: <http://igr.poznan.pl/lmm/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

# What analysis was done for dataset 'example'?
# Returns models that were analysed for a paricular dataset. If multiple models are returned, the returned AIC/BIC criteria allow to compare their fit. Model formula and exploration of additional attributes allows to further analyse the model, and check how to rerun the analysis if needed.


select distinct
	?Dataset
	?Description 
	?model ?ModelFormula ?AIC ?BIC
where { 
    
    FILTER(regex(?Dataset, "example"))   ### <--------- HERE SPECIFY DATASET
    
    ?dataset rdfs:label ?Dataset.
    ?dataset rdf:type obo:IAO_0000100. #datset
    optional {
        ?dataset dc:description ?Description.
    }
    ?modelfitting obo:OBI_0000293 ?dataset. #hasInput
	?modelfitting rdf:type obo:STATO_0000218. #modelFitting
    ?modelfitting obo:OBI_0000293 ?model. #hasInput
    ?model rdf:type obo:STATO_0000464. #mixedModel
    optional {
    	?model obo:STATO_0000205/rdf:value ?ModelFormula. #isDenotedBy
    }
    optional {
		?critBIC obo:IAO_0000136 ?model. #isAbout
        ?critBIC rdf:type obo:STATO_0000327. #BICcriterion
	    ?critBIC rdf:value ?BIC.
	}
    optional {
        ?critAIC obo:IAO_0000136 ?model. #isAbout
        ?critAIC rdf:type obo:STATO_0000325. #AICcriterion
	    ?critAIC rdf:value ?AIC.
	}

}